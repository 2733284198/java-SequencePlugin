
plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.7.2"
    id "org.jetbrains.changelog" version "1.1.1"
}

intellij {
    version '2020.2'
    pluginName 'SequenceDiagram'
    plugins = ['com.intellij.java','org.jetbrains.kotlin','org.intellij.plugins.markdown']
    updateSinceUntilBuild true
    sandboxDirectory = "${project.rootDir}/.sandbox"
    jreRepo 'https://jetbrains.bintray.com/intellij-jbr'
}

patchPluginXml {
    sinceBuild '202'
    untilBuild '211.*'
    pluginDescription({
        def start = "<!-- Plugin description -->"
        def end = "<!-- Plugin description end -->"
        def lines = new File("${project.projectDir}/README.md").collect {it}
        def str = lines.subList(lines.indexOf(start) + 1, lines.indexOf(end)).join("<br>")
        str
    })
    changeNotes({ changelog.getLatest().toHTML() })
}

publishPlugin {
    token System.getenv("PUGLISH_TOKEN")
}

group 'vanstudio'
version '2.1.a2'

//some jdk11 functions in codes
sourceCompatibility = 11

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    jcenter()
}