
plugins {
    id 'java'
    id "org.jetbrains.intellij" version "0.6.5"
    id "org.jetbrains.changelog" version "1.0.0"
}

intellij {
    version '2020.1'
    pluginName 'SequenceDiagram'
    plugins = ['com.intellij.java','org.jetbrains.kotlin']
    updateSinceUntilBuild true
    sandboxDirectory = "${project.rootDir}/.sandbox"
    jreRepo 'https://jetbrains.bintray.com/intellij-jbr'
}

patchPluginXml {
    sinceBuild '201'
    untilBuild '211.*'
    pluginDescription({
        def start = "<!-- Plugin description -->"
        def end = "<!-- Plugin description end -->"
        def lines = new File("./README.md").collect {it}
        def str = lines.subList(lines.indexOf(start) + 1, lines.indexOf(end)).join("<br>")
        str
    })
    changeNotes({ changelog.getLatest().toHTML() })
}

publishPlugin {
    token System.getenv("PUGLISH_TOKEN")
}

group 'vanstudio'
version '2.1.a1'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    jcenter()
}